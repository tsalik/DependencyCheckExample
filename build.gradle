// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.4.0"
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.1.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.owasp:dependency-check-gradle:6.0.5'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

subprojects {
    afterEvaluate {
        check.dependsOn dependencyCheckAggregate
    }
}

apply plugin: 'org.owasp.dependencycheck'
dependencyCheck {
    def skipConfigurationPatterns = [
            "lintClassPath",
            "kotlinCompiler.*"
    ]

    allprojects {
        configurations.all { configuration ->
            if (configuration.name in skipConfigurations) {
                return
            }
            skipConfigurationPatterns.each { pattern ->
                if (configuration.name.matches(pattern)) {
                    skipConfigurations << configuration.name
                }
            }
        }
    }

    suppressionFile = 'dependency-check-suppressions.xml'
    failBuildOnCVSS = 5
}

task clean(type: Delete) {
    delete rootProject.buildDir
}